// Never Forget: Comments can "lie", but code not so much ;P
//-------------------------------------------------//
// Contains self defined dialog.tlk functionalities//
//-------------------------------------------------//



// Purpose: Works like RESOLVE_STR_REF, but does so by taking only string values
// 			for resolving text and soundfile name.
// ------------------------------------------------------------------------------
// Parameters:|
// ============
// voxText 	-> The Text found in dialog.tlk
// voxSound	-> The name of the soundfile without ".wav" found in the dialog.tlk 
// strRef	-> The string reference number which is found
DEFINE_DIMORPHIC_FUNCTION RESOLVE_STR_REF_WITH_STRINGS
    STR_VAR
        voxText 	= ~!_NULL~
		voxSound	= ~!_NULL~
	RET
		strRef
BEGIN
	// Checks if a proper voxText is given	
	ACTION_IF (~%voxText%~ STRING_EQUAL ~!_NULL~) THEN 
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION RESOLVE_STR_REF_WITH_STRINGS -> The correct argument for voxText is missing. The argument !_NULL is forbidden.~
	END
	// Checks if a proper voxSound is given
	ACTION_IF (~%voxSound%~ STRING_EQUAL ~!_NULL~) THEN 
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION RESOLVE_STR_REF_WITH_STRINGS -> The correct argument for voxSound is missing. The argument !_NULL is forbidden.~
	END
	
	// Core part of this function. This part takes the string parameters 
	// and uses them as allowed in RESOLVE_STR_REF.
	<<<<<<<<./weidu_external/temp/!_TemporaryResolveStrRef.tpa
	OUTER_SET strRef = RESOLVE_STR_REF(~%voxText%~ [%voxSound%])
	>>>>>>>>
	COPY ~./weidu_external/temp/!_TemporaryResolveStrRef.tpa~ ~./weidu_external/temp/values/!_TemporaryResolveStrRef.tpa~ EVALUATE_BUFFER
	REINCLUDE ~./weidu_external/temp/values/!_TemporaryResolveStrRef.tpa~
	
END// End of "RESOLVE_STR_REF_WITH_STRINGS"



// Purpose: Gets the highest strref value by iterative counting through dialog.tlk
// -------------------------------------------------------------------------------
// Parameters:|
// ============
// strRef -> highest string reference value in dialog.tlk
DEFINE_DIMORPHIC_FUNCTION HIGHEST_STR_REF_BY_COUNTING
	RET
		strRef
BEGIN
	// Return variable
	OUTER_SET strRef = 0
	// Loops over dialog.tlk
	ALTER_TLK BEGIN	
		SET strRef = strRef+1
	END
	// Reducing the value by 1, to get correct value 
	OUTER_SET strRef = strRef - 1
	
END// End of "HIGHEST_STR_REF_BY_COUNTING"